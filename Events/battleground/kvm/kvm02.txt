/*.-----------------------------------------------------------------------------.
  .          ____                          __                                   .
  .         /\  _`\                       /\ \__  __                            .
  .         \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __            .
  .          \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\          .
  .           \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/          .
  .            \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\         .
  .             \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/         .
  .                                                                             .
  .                  2014~2016 ï¿½ Creative Services and Developent               .
  .                            www.creativesd.com.br                            .
  .                          www.sbkmod.ragnagreen.com                          .
  .-----------------------------------------------------------------------------.
  . Script:                                                                     .
  .    2ï¿½ Arena de Krieger von Midgard                                          .
  .-----------------------------------------------------------------------------.
  . Autor: Romulo SM (sbk_) (HerculesWS)                           Versï¿½o: 6.0  .
  .                                                                             .
  . Compï¿½tivel:	eAthena e derivaï¿½ï¿½es.                               	        .
  .-----------------------------------------------------------------------------.
  . Descriï¿½ï¿½o:                                                                  .
  .    Arena no qual os Exï¿½rcitos se confrontam diretamente.                    .
  *------------------------------------------------------------------------------*/

// Engine das Batalhas Campais de Krieger von Midgard
// -------------------------------------------------------
bat_room,150,125,5	script	Time Gai#a2::KvM02R_Guillaume	60,{
	end;
OnInit:
	waitingroom "5 jogadores para iniciar!",6,"KvM02_BG_Out::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("bat_c02",52,129,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bat_room,158,125,3	script	Time Kakashi#b2::KvM02R_Croix	843,{
	end;
OnInit:
	waitingroom "5 jogadores para iniciar!",6,"KvM02_BG_Out::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("bat_c02",147,55,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

// Starting Line
//============================================================
-	script	KVM02::CellEffect2	-1,{
	end;
OnKvM02One:
	specialeffect EF_CHANGECOLD;
	end;
OnKvM02Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	139
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	139
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	139
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	139
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	139
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	139
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	139

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	139
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	139
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	139
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	139
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	139
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	139
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	139

bat_c02,54,128,3	script	#A_camp_start02	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start02";
	end;
OnEnable:
	enablenpc "#A_camp_start02";
	end;
OnDisable:
	disablenpc "#A_camp_start03";
	end;
OnTouch:
	set Bat_Team,1;
	end;
}

bat_c02,146,56,3	script	#B_camp_start02	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start02";
	end;
OnEnable:
	enablenpc "#B_camp_start02";
	end;
OnDisable:
	disablenpc "#B_camp_start02";
	end;
OnTouch:
	set Bat_Team,2;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	disablenpc "Nara Shikamaru#KVM02A";
	disablenpc "Nara Shikamaru#KVM02B";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
		else {
			mapannounce "bat_c02", "O número de sobreviventes do time Gai é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			sleep 500;
			mapannounce "bat_c02", "O número de sobreviventes do time Kakashi é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O número de sobreviventes do time Gai é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			sleep 500;
			mapannounce "bat_c02", "O número de sobreviventes do time Kakashi é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnStart:
	disablenpc "Nara Shikamaru#KVM02A";
	disablenpc "Nara Shikamaru#KVM02B";
	set $@KvM02BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Em 1 minuto, a Ranqueada vai começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O tempo limite de batalha é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Se preparem!",bc_map,"0x00ff00";
	donpcevent "#A_camp_start02::OnEnable";
	donpcevent "#B_camp_start02::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start02::OnDisable";
	donpcevent "#B_camp_start02::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c02", "30 segundos para o início da partida.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c02", "15 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02One";
	end;

OnTimer50000:
	mapannounce "bat_c02", "10 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02Two";
	end;

OnTimer55000:
	mapannounce "bat_c02", "5 segundos para o início da partida.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02One";
	end;

OnTimer59000:
	mapannounce "bat_c02", "A partida iniciou!",bc_map,"0x00ff00";
	donpcevent "::OnKvM02Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "bat_c02","Não temos shinobis suficientes para o início.",1,0x808080;
		stopnpctimer;
		donpcevent "KvM02_BG::OnStop";
		end;
	}
	set $@KvM02BG, 2; // Playing
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c02", "1 minuto para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c02", "30 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c02", "15 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c02", "10 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c02", "5 segundos para o fim da partida.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c02", "A partida acabou!",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		mapannounce "bat_c02", "O número de sobreviventes do time Gai é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		sleep 500;
		mapannounce "bat_c02", "O número de sobreviventes do time Kakashi é "+.Croix_Count+".",bc_map,"0x00ff00";
		sleep 500;
		mapannounce "bat_c02", "Foi um empate!",bc_map,"0x00ff00";
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	mapannounce "bat_c02", "Time Gai venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns a todos!",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	mapannounce "bat_c02", "Time Kakashi venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns a todos!",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "Nara Shikamaru#KVM02A";
	enablenpc "Nara Shikamaru#KVM02B";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Fale com Shikamaru para ganhar a premiação!",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "Em 30 segundos, o oficial será desativado!",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "Você só ganhará o prêmio se falar com o oficial, cuidado!",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c02", "Partida encerrada, todos voltaram para a sala.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set getvariableofnpc(.Croix_Count,"KvM02_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM02_BG"), 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "Nara Shikamaru#KVM02A";
	disablenpc "Nara Shikamaru#KVM02B";
	mapwarp "bat_c02","bat_room",154,150;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",5,5;
	set $@KvM02BG, 0;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM02BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c02,51,130,5	script	Nara Shikamaru#KVM02A	65,{
	if( $@KvM02BG_Victory ) {
		mes "[KVM Officer]";
		if( $@KvM02BG_Victory == Bat_Team ) {
			mes "Bom jogo, você é o vencedor!";
			mes "Você ganhou 30 pontos!";
			getitem 735,30;
			close2;
		}
		else {
			mes "Boa sorte na próxima!";
			mes "Apesar da derrota, você ganhou 15 pontos!";
			getitem 735,15;
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
}
bat_c02,148,53,1	duplicate(Nara Shikamaru#KVM02A)	Nara Shikamaru#KVM02B	65
